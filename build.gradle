apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'br.net.bsocial.db'
version = 'Spring-2015'
description = 'replicated lmdb: a lmdb + leveldb + bookkeeper experiment'

idea {
    module {
        downloadJavadoc = false
        downloadSources = true
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

dependencies {
    compile 'org.deephacks.lmdbjni:lmdbjni-linux64:0.4.4.1'
    compile 'org.fusesource.leveldbjni:leveldbjni-linux64:1.8'
    compile 'org.apache.bookkeeper:bookkeeper-server:4.3.1'
    compile 'org.apache.curator:curator-framework:2.9.1'
    compile 'org.apache.curator:curator-recipes:2.9.1'
}

jar {

    manifest.attributes (
        'Main-Class': 'rlmdb.Env',
        'Class-Path': configurations.runtime.files.collect { 'lib/'+it.name }.join(' ')
    )

    doLast {

        mkdir './lib'

        copy {
            from "build/libs/$archiveName"
            rename archiveName, "$baseName.$extension"
            into "./"
        }

        copy {
            from configurations.runtime
            into "./lib"
        }
    }
}

clean {
    delete "./lib"
    delete "./$jar.baseName.$jar.extension"
}


